! Morphological Transducer for Yiddish

Multichar_Symbols

! Part of speech categories
%<n%>    ! Noun
%<v%>    ! Verb
%<det%>  ! determiner
%<prn%>  ! pronoun

! number: for nouns, verbs, adjectives, determiners, etc.
%<pl%>   ! Plural
%<sg%>   ! Singular

! gender: for nouns, determiners, adjectives, etc.
%<m%>    ! masculine
%<f%>    ! feminine
%<nt%>   ! neuter

! person
%<p1%>
%<p2%>
%<p3%>

! cases: for determiners, etc.
%<nom%>  ! nominative
%<acc%>  ! accusative
%<dat%>  ! dative

! verb forms
%<inf%>  ! infinitive
%<pres%> ! present
%<pp%>   ! past participle
%<pprs%> ! present participle
%<imp%>  ! imperative

! Escaped characters

%{ь%}    ! used for umlaut of ~previous vowel
%>       ! morpheme boundary
%{ױ%}    ! for ױ to ײ umlaut correspondence

! matched features
%[%+pp%] ! past participle
%[%-pp%] ! past participle

! Regular umlaut correspondences:
! ו - י
! אָ - ע
! ױ - ײ/ײַ
! אַ - ע
אַ
אָ
ײַ
יִ
וּ
בּ
בֿ
כּ
כֿ
ךֿ
פֿ
פּ

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NounRoot ;
DetRoot ;
VerbRoot ;


!!! Format for noun lexica is N-[gender]-[class]
!!! gender: M, F, N
!!! class: UML, UML-ER, S, N, NULL, HEB

LEXICON N-M-UML

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:%{ь%} # ;

LEXICON N-F-UML

%<n%>%<f%>%<sg%>: # ;
%<n%>%<f%>%<pl%>:%{ь%} # ;

LEXICON N-M-UML-ER

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:%>%{ь%}ער # ;

LEXICON N-F-UML-ER

%<n%>%<f%>%<sg%>: # ;
%<n%>%<f%>%<pl%>:%>%{ь%}ער # ;

LEXICON N-N-UML-ER

%<n%>%<nt%>%<sg%>: # ;
%<n%>%<nt%>%<pl%>:%>%{ь%}ער # ;

LEXICON N-M-S

%<n%>%<m%>%<sg%>: # ;

LEXICON N-F-S

%<n%>%<f%>%<sg%>: # ;
%<n%>%<f%>%<pl%>:%>ס # ;

LEXICON N-N-S

%<n%>%<nt%>%<sg%>: # ;
%<n%>%<nt%>%<pl%>:%>ס # ;

LEXICON N-M-N

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:%>ס # ;

LEXICON N-F-N

%<n%>%<f%>%<sg%>: # ;

LEXICON N-N-N

%<n%>%<nt%>%<sg%>: # ;

LEXICON N-M-NULL

%<n%>%<m%>%<sg%>: # ;

LEXICON N-F-NULL

%<n%>%<f%>%<sg%>: # ;

LEXICON N-N-NULL

%<n%>%<nt%>%<sg%>: # ;

LEXICON N-M-HEB

%<n%>%<m%>%<sg%>: # ;

LEXICON N-F-HEB

%<n%>%<f%>%<sg%>: # ;


LEXICON DET-DEF-M

%<det%>%<m%>%<nom%>:דער # ;
%<det%>%<m%>%<acc%>:דעם # ;
%<det%>%<m%>%<dat%>:דעם # ;

LEXICON DET-DEF-F

%<det%>%<f%>%<nom%>:די # ;
%<det%>%<f%>%<acc%>:די # ;
%<det%>%<f%>%<dat%>:דער # ;

LEXICON DET-DEF-N

%<det%>%<nt%>%<nom%>:דאָס # ;
%<det%>%<nt%>%<acc%>:דאָס # ;
%<det%>%<nt%>%<dat%>:דעם # ;

LEXICON DET-DEF-PL

%<det%>%<pl%>%<nom%>:די # ;
%<det%>%<pl%>%<acc%>:די # ;
%<det%>%<pl%>%<dat%>:די # ;

LEXICON V-COMMON

%<v%>%<inf%>:%>ן # ;

%<v%>%<pres%>%<p1%>%<sg%>: # ;
%<v%>%<pres%>%<p2%>%<sg%>:%>סט # ;
%<v%>%<pres%>%<p3%>%<sg%>:%>ט # ;
%<v%>%<pres%>%<p1%>%<pl%>:%>ן # ;
%<v%>%<pres%>%<p2%>%<pl%>:%>ט # ;
%<v%>%<pres%>%<p3%>%<pl%>:%>ן # ;

%<v%>%<imp%>%<sg%>: # ;
%<v%>%<imp%>%<pl%>:%>ט # ;

%<v%>%<pprs%>:%>נדיק # ;


LEXICON V-REG
! Plain old weak verbs

V-COMMON ;
%<v%>%<pp%>%[%+pp%]:%>ט # ;


LEXICON V-REG-NOPREF
! Weak verbs that take no prefix

V-COMMON ;
%<v%>%<pp%>:%>ט # ;


LEXICON V-STRONG-7
! class 7 strong verbs

V-COMMON ;
%<v%>%<pp%>%[%+pp%]:%>ן # ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounRoot

הױז:הױז N-N-UML-ER ; ! "house" plural with ײַ
בּױם:בּ%{ױ%}ם N-M-UML-ER ; ! "tree" plural with ײ
אַרבעט:אַרבעט N-F-S ; ! "work"
בּאַרג:בּאַרג N-M-UML ; ! "mountain"

LEXICON DetRoot

דער: DET-DEF-M ; ! "the"
די: DET-DEF-F ; ! "the"
דאָס: DET-DEF-N ; ! "the"
די: DET-DEF-PL ; ! "the"

LEXICON VerbRoot
%[%-pp%]:גע VerbStems ; ! past participle
VerbStems ; ! other forms


LEXICON VerbStems

קױפֿן:קױפֿ V-REG ;
פֿאַרלירן:פֿאַרליר V-REG-NOPREF ;
בּעטן:בּעט V-STRONG-7 ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! http://hfst.sourceforge.net/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst

