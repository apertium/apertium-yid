! Morphological Transducer for Yiddish

Multichar_Symbols

! Part of speech categories
%<n%>    ! Noun
%<v%>    ! Verb
%<det%>  ! determiner
%<prn%>  ! pronoun
%<cnjcoo%> ! coordinating conjunction
%<pr%>   ! preposition

! types of pronouns
%<pers%> ! personal

! number: for nouns, verbs, adjectives, determiners, etc.
%<pl%>   ! Plural
%<sg%>   ! Singular

! gender: for nouns, determiners, adjectives, etc.
%<m%>    ! masculine
%<f%>    ! feminine
%<nt%>   ! neuter

! person
%<p1%>   ! 1st person
%<p2%>   ! 2nd person
%<p3%>   ! 3rd person
%<impers%> ! impersonal

! cases: for determiners, pronouns, etc.
%<nom%>  ! nominative
%<acc%>  ! accusative
%<dat%>  ! dative

! verb forms
%<inf%>  ! infinitive
%<pres%> ! present
%<pp%>   ! past participle
%<pprs%> ! present participle
%<imp%>  ! imperative

! Escaped characters

%{ь%}    ! used for umlaut of ~previous vowel
%>       ! morpheme boundary
%{ױ%}    ! for ױ to ײ umlaut correspondence
%{ע%}    ! ע or empty; in -{ע}ן is empty except after נ and ל

! matched features
%[%+pp%] ! past participle
%[%-pp%] ! past participle

! Regular umlaut correspondences:
! ו - י
! אָ - ע
! ױ - ײ/ײַ
! אַ - ע
אַ
אָ
ײַ
יִ
וּ
בּ
בֿ
כּ
כֿ
ךֿ
פֿ
פּ

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NounRoot ;
DetRoot ;
VerbRoot ;
Conjunctions ;
Prepositions ;
Pronouns ;


!!! Format for noun lexica is N-[gender]-[class]
!!! gender: M, F, N
!!! class: UML, UML-ER, S, N, NULL, HEB

LEXICON N-M-UML

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:%{ь%} # ;

LEXICON N-F-UML

%<n%>%<f%>%<sg%>: # ;
%<n%>%<f%>%<pl%>:%{ь%} # ;

LEXICON N-M-UML-ER

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:%>%{ь%}ער # ;

LEXICON N-F-UML-ER

%<n%>%<f%>%<sg%>: # ;
%<n%>%<f%>%<pl%>:%>%{ь%}ער # ;

LEXICON N-N-UML-ER

%<n%>%<nt%>%<sg%>: # ;
%<n%>%<nt%>%<pl%>:%>%{ь%}ער # ;

LEXICON N-M-S

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:%>ס # ;

LEXICON N-F-S

%<n%>%<f%>%<sg%>: # ;
%<n%>%<f%>%<pl%>:%>ס # ;

LEXICON N-N-S

%<n%>%<nt%>%<sg%>: # ;
%<n%>%<nt%>%<pl%>:%>ס # ;

LEXICON N-M-N

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:%>%{ע%}ן # ;

LEXICON N-F-N

%<n%>%<f%>%<sg%>: # ;

LEXICON N-N-N

%<n%>%<nt%>%<sg%>: # ;

LEXICON N-M-NULL

%<n%>%<m%>%<sg%>: # ;

LEXICON N-F-NULL

%<n%>%<f%>%<sg%>: # ;

LEXICON N-N-NULL

%<n%>%<nt%>%<sg%>: # ;

LEXICON N-M-HEB

%<n%>%<m%>%<sg%>: # ;

LEXICON N-F-HEB

%<n%>%<f%>%<sg%>: # ;


LEXICON DET-DEF-M

%<det%>%<m%>%<nom%>:ער # ;
%<det%>%<m%>%<acc%>:עם # ;
%<det%>%<m%>%<dat%>:עם # ;

LEXICON DET-DEF-F

%<det%>%<f%>%<nom%>:י # ;
%<det%>%<f%>%<acc%>:י # ;
%<det%>%<f%>%<dat%>:ער # ;

LEXICON DET-DEF-N

%<det%>%<nt%>%<nom%>:אָס # ;
%<det%>%<nt%>%<acc%>:אָס # ;
%<det%>%<nt%>%<dat%>:עם # ;

LEXICON DET-DEF-PL

%<det%>%<pl%>%<nom%>:י # ;
%<det%>%<pl%>%<acc%>:י # ;
%<det%>%<pl%>%<dat%>:י # ;

LEXICON DET-INDEF

%<det%>%<sg%>: # ;
! FIXME: deal with alternate אַן

LEXICON V-COMMON

%<v%>%<pres%>%<p1%>%<sg%>: # ;
%<v%>%<pres%>%<p2%>%<sg%>:%>סט # ;
%<v%>%<pres%>%<p3%>%<sg%>:%>ט # ;
%<v%>%<pres%>%<p1%>%<pl%>:%>ן # ;
%<v%>%<pres%>%<p2%>%<pl%>:%>ט # ;
%<v%>%<pres%>%<p3%>%<pl%>:%>ן # ;

%<v%>: V-COMMON-INF ;
%<v%>: V-COMMON-IMP ;
%<v%>: V-COMMON-PPRS ;

LEXICON V-COMMON-INF

%<inf%>:%>ן # ;

LEXICON V-COMMON-PPRS

%<pprs%>:%>%{ע%}נדיק # ;

LEXICON V-COMMON-IMP

V-COMMON-IMP-SG ;
V-COMMON-IMP-PL ;

LEXICON V-COMMON-IMP-SG

%<imp%>%<sg%>: # ;

LEXICON V-COMMON-IMP-PL

%<imp%>%<pl%>:%>ט # ;


LEXICON V-REG
! Plain old weak verbs

V-COMMON ;
%<v%>%<pp%>%[%+pp%]:%>ט # ;


LEXICON V-REG-NOPREF
! Weak verbs that take no prefix

V-COMMON ;
%<v%>%<pp%>:%>ט # ;


LEXICON V-STRONG-7
! class 7 strong verbs

V-COMMON ;
%<v%>%<pp%>%[%+pp%]:%>ן # ;


LEXICON V-ZAYN-ZAY

%<v%>%<pres%>%<p1%>%<pl%>:%>נען # ;
%<v%>%<pres%>%<p2%>%<pl%>:%>ט # ;
%<v%>%<pres%>%<p3%>%<pl%>:%>נען # ;

%<v%>: V-COMMON-INF ;
%<v%>: V-COMMON-IMP ;
%<v%>: V-COMMON-PPRS ;

LEXICON V-ZAYN-BI

%<v%>%<pres%>%<p1%>%<sg%>:%>ן # ;
%<v%>%<pres%>%<p2%>%<sg%>:%>סט # ;

LEXICON V-ZAYN-IZ

%<v%>%<pres%>%<p3%>%<sg%>: # ;

LEXICON V-ZAYN-VE

%<v%>%<pp%>%[%+pp%]:ן # ;
%<v%>%<pp%>%[%+pp%]:זן # ;  ! Dir/LR

LEXICON V-HOBN-HOB

%<v%>: V-COMMON-INF ;
%<v%>: V-COMMON-PPRS ;
%<v%>: V-COMMON-IMP-SG ;

%<v%>%<pres%>%<p1%>%<sg%>: # ;
%<v%>%<pres%>%<p1%>%<pl%>:%>ן # ;
%<v%>%<pres%>%<p3%>%<pl%>:%>ן # ;

LEXICON V-HOBN-HO

%<v%>%<pres%>%<p2%>%<sg%>:%>סט # ;
%<v%>%<pres%>%<p3%>%<sg%>:%>ט # ;
%<v%>%<pres%>%<p2%>%<pl%>:%>ט # ;
%<v%>: V-COMMON-IMP-PL ;

LEXICON V-HOBN-HA

%<v%>%<pp%>%[%+pp%]:ט # ;

LEXICON CC

%<cnjcoo%>: # ;

LEXICON PREP

%<pr%>: # ;

LEXICON PRON-P1-SG
איך%<prn%>%<pers%>%<p1%>%<sg%>%<nom%>:איך # ;
איך%<prn%>%<pers%>%<p1%>%<sg%>%<acc%>:מיך # ;
איך%<prn%>%<pers%>%<p1%>%<sg%>%<dat%>:מיר # ;

LEXICON PRON-P2-SG
דו%<prn%>%<pers%>%<p2%>%<sg%>%<nom%>:דו # ;
דו%<prn%>%<pers%>%<p2%>%<sg%>%<acc%>:דיך # ;
דו%<prn%>%<pers%>%<p2%>%<sg%>%<dat%>:דיר # ;

LEXICON PRON-P3-SG
ער%<prn%>%<pers%>%<p3%>%<sg%>%<m%>%<nom%>:ער # ;
ער%<prn%>%<pers%>%<p3%>%<sg%>%<m%>%<acc%>:אים # ;
ער%<prn%>%<pers%>%<p3%>%<sg%>%<m%>%<dat%>:אים # ;

זי%<prn%>%<pers%>%<p3%>%<sg%>%<f%>%<nom%>:זי # ;
זי%<prn%>%<pers%>%<p3%>%<sg%>%<f%>%<acc%>:זי # ;
זי%<prn%>%<pers%>%<p3%>%<sg%>%<f%>%<dat%>:איר # ;

עס%<prn%>%<pers%>%<p3%>%<sg%>%<nt%>%<nom%>:עס # ;
עס%<prn%>%<pers%>%<p3%>%<sg%>%<nt%>%<acc%>:עס # ;
עס%<prn%>%<pers%>%<p3%>%<sg%>%<nt%>%<dat%>:אים # ;

LEXICON PRON-P1-PL
מיר%<prn%>%<pers%>%<p1%>%<pl%>%<nom%>:מיר # ;
מיר%<prn%>%<pers%>%<p1%>%<pl%>%<acc%>:אונדז # ;
מיר%<prn%>%<pers%>%<p1%>%<pl%>%<dat%>:אונדז # ;

LEXICON PRON-P2-PL
איר%<prn%>%<pers%>%<p2%>%<pl%>%<nom%>:איר # ;
איר%<prn%>%<pers%>%<p2%>%<pl%>%<acc%>:אײַך # ;
איר%<prn%>%<pers%>%<p2%>%<pl%>%<dat%>:אײַך # ;

LEXICON PRON-P3-PL
זײ%<prn%>%<pers%>%<p3%>%<pl%>%<nom%>:זײ # ;
זײ%<prn%>%<pers%>%<p3%>%<pl%>%<acc%>:זײ # ;
זײ%<prn%>%<pers%>%<p3%>%<pl%>%<dat%>:זײ # ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Conjunctions

און:און CC ; ! "and"

LEXICON Prepositions

פֿון:פֿון PREP ; ! "of, from"
אין:אין PREP ; ! "in"


LEXICON DetRoot

דער:ד DET-DEF-M ; ! "the"
די:ד DET-DEF-F ; ! "the"
דאָס:ד DET-DEF-N ; ! "the"
די:ד DET-DEF-PL ; ! "the"
אַ:אַ DET-INDEF ; ! "a"
!אַן:אַן DET-INDEF ; ! "an"

LEXICON Pronouns

PRON-P1-SG ;
PRON-P2-SG ;
PRON-P3-SG ;
PRON-P1-PL ;
PRON-P2-PL ;
PRON-P3-PL ;
מען%<prn%>%<pers%>%<impers%>%<nom%>:מען # ;
מען%<prn%>%<pers%>%<impers%>%<nom%>:מע # ;  ! Dir/LR

LEXICON NounRoot

הױז:הױז N-N-UML-ER ; ! "house" plural with ײַ
בּױם:בּ%{ױ%}ם N-M-UML-ER ; ! "tree" plural with ײ
אַרבעט:אַרבעט N-F-S ; ! "work"
בּאַרג:בּאַרג N-M-UML ; ! "mountain"
יאָר:יאָר N-M-N ; ! "year"
הימל:הימל N-M-N ; ! "heaven"
לאַן:לאַן N-M-N ; ! "meadow"

LEXICON VerbRoot
%[%-pp%]:גע VerbStems ; ! past participle
VerbStems ; ! other forms

LEXICON IrregularVerbs

זײַן:זײַ V-ZAYN-ZAY ;
זײַן:בי V-ZAYN-BI ;
זײַן:איז V-ZAYN-IZ ;
זײַן:װע V-ZAYN-VE ;

האָבן:האָב V-HOBN-HOB ;
האָבן:האַ V-HOBN-HO ;
האָבן:האַ V-HOBN-HA ;

LEXICON VerbStems

IrregularVerbs ; 
קױפֿן:קױפֿ V-REG ;
פֿאַרלירן:פֿאַרליר V-REG-NOPREF ;
בּעטן:בּעט V-STRONG-7 ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! http://hfst.sourceforge.net/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst

