! Morphological Transducer for Yiddish

Multichar_Symbols

! Part of speech categories
%<n%>    ! Noun
%<v%>    ! Verb
%<vaux%> ! Auxiliary
%<det%>  ! Determiner
%<prn%>  ! Pronoun
%<pr%>   ! Preposition
%<det%>  ! Determiner
%<adv%>  ! Adverb
%<adj%>  ! Adjective
%<preadv%> ! Preadverb
%<cnjcoo%> ! Coordinating conjunction
%<cnjsub%> ! Subordinating conjunction
%<cnjadv%> ! Conjunctive adverb
%<np%>   ! Proper noun
%<title%>  ! title
%<num%>  ! Numeral

! types of pronouns, determiners, and adjectives
%<pers%> ! personal
%<pos%>  ! personal possessive
%<ref%>  ! reflexive
%<attr%> ! attributive ("prepositive")
%<pred%> ! predicative ("postpositive", nominalised)
%<itg%>  ! interrogative
%<comp%> ! Comparative
%<sup%>  ! Superlative
%<qnt%>  ! Quantifier

! number: for nouns, verbs, adjectives, determiners, etc.
%<pl%>   ! Plural
%<sg%>   ! Singular

! definiteness
%<def%>  ! Definite
%<ind%>  ! Indefinite

! gender: for nouns, determiners, adjectives, etc.
%<m%>    ! masculine
%<f%>    ! feminine
%<nt%>   ! neuter
%<mfn%>  ! masculine/feminine/neuter

! person
%<p1%>   ! 1st person
%<p2%>   ! 2nd person
%<p3%>   ! 3rd person
%<impers%> ! impersonal

! cases: for determiners, pronouns, etc.
%<nom%>  ! nominative
%<acc%>  ! accusative
%<dat%>  ! dative

! verb forms
%<inf%>  ! infinitive
%<pres%> ! present
%<pp%>   ! past participle
%<pprs%> ! present participle
%<imp%>  ! imperative

! Proper noun types
%<top%>  ! topographic (places)
%<ant%>  ! anthroponyms (given names)
%<cog%>  ! cognomens (family names)
%<org%>  ! organisations

! Punctuation
%<percent%>   ! Percent
%<sent%>      ! Sentence marker
%<guio%>      ! Hyphen
%<cm%>        ! Comma
%<apos%>      ! Apostrophe
%<rquot%>     ! Quote marker (right hand side)
%<lquot%>     ! Quote marker (left hand side)
%<rpar%>      ! Parenthetical marker (right hand side)
%<lpar%>      ! Parenthetical marker (left hand side)
%<ltr%>       ! Letter

! Misc

%<ord%>       ! Ordinal

! Escaped characters

%>       ! morpheme boundary
%.
%,

№
%-
%  
%+
%−

%{ь%}    ! used for umlaut of ~previous vowel for verbs
%{ь2%}   ! used for umlaut of ~previous vowel for adjectives
%{ױ%}    ! for ױ to ײ umlaut correspondence
%{ע%}    ! ע or empty; in -{ע}ן is empty except after נ and ל
%{ס%}    ! ס in סט superlative, deletes after other ס
%{ן%}    ! ן sometimes ם

! matched features
%[%+pp%] ! past participle
%[%-pp%] ! past participle

%[%+→on%]
%[%-→on%]
%[%+←on%]
%[%-←on%]
%[√on%]


! Regular umlaut correspondences:
! ו - י
! אָ - ע
! ױ - ײ/ײַ
! אַ - ע
אַ
אָ
ײַ
יִ
וּ
בּ
בֿ
כּ
כֿ
פֿ
פּ
תּ
יי
וו
וי
ייִ


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NounRoot ;
DetRoot ;
VerbRoot ;
Conjunctions ;
Prepositions ;
Pronouns ;
Punctuation ;
Adverbs ;
Adjectives ;
ProperNouns ;
Titles ;
Numerals ;
Digits ;     ! Use/Circ


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!==============================================================================!
!
!                               N U M E R A L S
!
!    WARNING: This section should not be modified without consultation.
!             Test cases are not yet in dev/numerals.
!==============================================================================!

LEXICON SQRD
! This lexicon includes common ways of writing squared, cubed etc.

²%<num%>:2 # ;  ! Dir/LR
³%<num%>:3 # ;  ! Dir/LR
²%<num%>:² # ;
³%<num%>:³ # ;
%<num%>: # ;

LEXICON NUM-DIGIT

%<num%>: # ;

! FIXME: find some way to make these generate!
%<num%>%<ord%>:%>סט ADJ-INFL-ENDINGS ; ! Dir/LR
%<num%>%<ord%>:%>ט ADJ-INFL-ENDINGS ;  ! Dir/LR

%<num%>%<percent%>:%% # ;

°С%<num%>:°С # ;
°С%<num%>:ºС # ; ! Dir/LR
°С%<num%>:С # ; ! Dir/LR

°%<num%>:° # ; ! Dir/LR


! Measurement units

ק%"מ:ק%"מ SQRD ;  ! "kilometres"
                ! FIXME: add more

LEXICON LASTDIGIT

1:1                     NUM-DIGIT ; !
2:2                     NUM-DIGIT ; !
3:3                     NUM-DIGIT ; !
4:4                     NUM-DIGIT ; !
5:5                     NUM-DIGIT ; !
6:6                     NUM-DIGIT ; !
7:7                     NUM-DIGIT ; !
8:8                     NUM-DIGIT ; !
9:9                     NUM-DIGIT ; !

LEXICON LASTDIGIT-REST

1%0:1%0                 NUM-DIGIT ; !
2%0:2%0                 NUM-DIGIT ; !
3%0:3%0                 NUM-DIGIT ; !
4%0:4%0                 NUM-DIGIT ; !
5%0:5%0                 NUM-DIGIT ; !
6%0:6%0                 NUM-DIGIT ; !
7%0:7%0                 NUM-DIGIT ; !
8%0:8%0                 NUM-DIGIT ; !
9%0:9%0                 NUM-DIGIT ; !

LEXICON POWERS

%0%0:%0%0               NUM-DIGIT ; !
%0%0%0:%0%0%0           ִNUM-DIGIT ; !

LEXICON LOOP

%,:%, DIGITLEX ;
%.:%. DIGITLEX ;
%,:%, LASTDIGIT ;
%.:%. LASTDIGIT ;
%,:%, LASTDIGIT-REST ;
%.:%. LASTDIGIT-REST ;
%,:%, POWERS ;
%.:%. POWERS ;
      DIGITLEX ;
      LASTDIGIT ;
      LASTDIGIT-REST ;
      POWERS ;

LEXICON DIGITLEX

%0:%0 LOOP ;
1:1   LOOP ;
2:2   LOOP ;
3:3   LOOP ;
4:4   LOOP ;
5:5   LOOP ;
6:6   LOOP ;
7:7   LOOP ;
8:8   LOOP ;
9:9   LOOP ;

!==============================================================================!
!          N U M E R A L     L E X I C O N     E N D S     H E R E
!==============================================================================!



!!! Format for noun lexica is N-[gender]-[class]
!!! gender: M, F, N
!!! class: UML, UML-ER, S, N, NULL, HEB

LEXICON N-M-UML

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:%{ь%} # ;

LEXICON N-F-UML

%<n%>%<f%>%<sg%>: # ;
%<n%>%<f%>%<pl%>:%{ь%} # ;

LEXICON N-M-UML-ER

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:%>%{ь%}ער # ;

LEXICON N-F-UML-ER

%<n%>%<f%>%<sg%>: # ;
%<n%>%<f%>%<pl%>:%>%{ь%}ער # ;

LEXICON N-N-UML-ER

%<n%>%<nt%>%<sg%>: # ;
%<n%>%<nt%>%<pl%>:%>%{ь%}ער # ;

LEXICON N-M-S

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:%>ס # ;

LEXICON N-F-S

%<n%>%<f%>%<sg%>: # ;
%<n%>%<f%>%<pl%>:%>ס # ;

LEXICON N-N-S

%<n%>%<nt%>%<sg%>: # ;
%<n%>%<nt%>%<pl%>:%>ס # ;

LEXICON N-M-N

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:%>%{ע%}ן # ;

LEXICON N-F-N

%<n%>%<f%>%<sg%>: # ;
%<n%>%<f%>%<pl%>:%{ע%}ן # ;

LEXICON N-N-N

%<n%>%<nt%>%<sg%>: # ;
%<n%>%<nt%>%<pl%>:%{ע%}ן # ;

LEXICON N-M-NULL

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>: # ;

LEXICON N-F-NULL

%<n%>%<f%>%<sg%>: # ;
%<n%>%<f%>%<pl%>: # ;

LEXICON N-N-NULL

%<n%>%<nt%>%<sg%>: # ;
%<n%>%<nt%>%<pl%>: # ;

LEXICON N-M-HEB

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:ים # ;

LEXICON N-M-HEB-N

%<n%>%<m%>%<sg%>: # ;
%<n%>%<m%>%<pl%>:נים # ;

LEXICON N-M-NOPL

%<n%>%<m%>%<sg%>: # ;

LEXICON N-F-NOPL

%<n%>%<f%>%<sg%>: # ;

LEXICON N-F-HEB

%<n%>%<f%>%<sg%>:ה # ;
%<n%>%<f%>%<pl%>:ות # ;

LEXICON N-M-REBE

%<n%>%<m%>%<sg%>%<nom%>: # ;
%<n%>%<m%>%<sg%>%<acc%>:ן # ;
%<n%>%<m%>%<sg%>%<dat%>:ן # ;
%<n%>%<m%>%<pl%>:ס # ;
%<n%>%<m%>%<pl%>:ים # ; ! (Hebrew plural) Dir/LR

LEXICON DET-DEF-M

%<det%>%<def%>%<m%>%<nom%>:ער # ;
%<det%>%<def%>%<m%>%<acc%>:עם # ;
%<det%>%<def%>%<m%>%<dat%>:עם # ;

LEXICON DET-DEF-F

%<det%>%<def%>%<f%>%<nom%>:י # ;
%<det%>%<def%>%<f%>%<acc%>:י # ;
%<det%>%<def%>%<f%>%<dat%>:ער # ;

LEXICON DET-DEF-N

%<det%>%<def%>%<nt%>%<nom%>:אָס # ;
%<det%>%<def%>%<nt%>%<acc%>:אָס # ;
%<det%>%<def%>%<nt%>%<dat%>:עם # ;

LEXICON DET-DEF-PL

%<det%>%<def%>%<pl%>%<nom%>:י # ;
%<det%>%<def%>%<pl%>%<acc%>:י # ;
%<det%>%<def%>%<pl%>%<dat%>:י # ;

LEXICON DET-INDEF

%<det%>%<ind%>%<sg%>: # ;
! FIXME: deal with alternate אַן

LEXICON V-FINAL-PARTICLE

# ;  ! non-particle verbs
%[%+→on%]:%#% אָן # ;

LEXICON V-INITIAL-PARTICLE

# ;  ! non-particle verbs
%[%+←on%]: # ;

LEXICON V-COMMON

%<v%>%<pres%>%<p1%>%<sg%>: V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p2%>%<sg%>:%>סט V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p3%>%<sg%>:%>ט V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p1%>%<pl%>:%>%{ע%}ן V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p2%>%<pl%>:%>ט V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p3%>%<pl%>:%>%{ע%}ן V-FINAL-PARTICLE ;

%<v%>: V-COMMON-INF ;
%<v%>: V-COMMON-IMP ;
%<v%>: V-COMMON-PPRS ;

LEXICON V-COMMON-INF

%<inf%>:%>%{ע%}ן V-INITIAL-PARTICLE ;

LEXICON V-COMMON-PPRS

%<pprs%>:%>%{ע%}נדיק V-INITIAL-PARTICLE ;

LEXICON V-COMMON-IMP

V-COMMON-IMP-SG ;
V-COMMON-IMP-PL ;

LEXICON V-COMMON-IMP-SG

%<imp%>%<sg%>: V-FINAL-PARTICLE ;

LEXICON V-COMMON-IMP-PL

%<imp%>%<pl%>:%>ט V-FINAL-PARTICLE ;


LEXICON V-REG
! Plain old weak verbs

V-COMMON ;
%<v%>%<pp%>%[%+pp%]:%>ט V-INITIAL-PARTICLE ;


LEXICON V-REG-NOPREF
! Weak verbs that take no prefix

V-COMMON ;
%<v%>%<pp%>:%>ט V-INITIAL-PARTICLE ;

LEXICON V-STRONG-NOPP

V-COMMON ;

LEXICON V-STRONG-PP-ONLY

%<v%>%<pp%>%[%+pp%]:%>%{ע%}ן V-INITIAL-PARTICLE ;

LEXICON V-STRONG-7
! class 7 strong verbs

V-COMMON ;
%<v%>%<pp%>%[%+pp%]:%>%{ע%}ן # ;

LEXICON V-GEYN-GEY

%<v%>: V-COMMON-INF ;

%<v%>%<pres%>%<p1%>%<sg%>: V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p2%>%<sg%>:%>סט V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p3%>%<sg%>:%>ט V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p2%>%<pl%>:%>ט V-FINAL-PARTICLE ;

%<v%>: V-COMMON-IMP ;

LEXICON V-GEYN-GEYE

%<v%>%<pres%>%<p1%>%<pl%>:%>%{ע%}ן V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p3%>%<pl%>:%>%{ע%}ן V-FINAL-PARTICLE ;

%<v%>: V-COMMON-PPRS ;

LEXICON V-TON-TU

%<v%>%<pres%>%<p1%>%<sg%>: V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p2%>%<sg%>:%>סט V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p3%>%<sg%>:%>ט V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p1%>%<pl%>:%>%{ע%}ן V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p2%>%<pl%>:%>ט V-FINAL-PARTICLE ;
%<v%>%<pres%>%<p3%>%<pl%>:%>%{ע%}ן V-FINAL-PARTICLE ;

%<v%>: V-COMMON-IMP ;
%<v%>: V-COMMON-PPRS ;

LEXICON V-TON-TO

%<v%>: V-COMMON-INF ;
%<v%>%<pp%>%[%+pp%]:%>%{ע%}ן V-INITIAL-PARTICLE ;

LEXICON V-ZAYN-ZE

%<v%>%<pres%>%<p1%>%<pl%>:%>נען # ;
%<v%>%<pres%>%<p3%>%<pl%>:%>נען # ;

LEXICON V-ZAYN-ZAY

%<v%>%<pres%>%<p1%>%<pl%>:%>נען # ;
%<v%>%<pres%>%<p2%>%<pl%>:%>ט # ;
%<v%>%<pres%>%<p3%>%<pl%>:%>נען # ;

%<v%>: V-COMMON-INF ;
%<v%>: V-COMMON-IMP ;
%<v%>: V-COMMON-PPRS ;

LEXICON V-ZAYN-BI

%<v%>%<pres%>%<p1%>%<sg%>:%>ן # ;
%<v%>%<pres%>%<p2%>%<sg%>:%>סט # ;

LEXICON V-ZAYN-IZ

%<v%>%<pres%>%<p3%>%<sg%>: # ;

LEXICON V-ZAYN-VE

%<v%>%<pp%>%[%+pp%]:ן # ;
%<v%>%<pp%>%[%+pp%]:זן # ;  ! Dir/LR

LEXICON V-HOBN-HOB

%<v%>: V-COMMON-INF ;
%<v%>: V-COMMON-PPRS ;
%<v%>: V-COMMON-IMP-SG ;

%<v%>%<pres%>%<p1%>%<sg%>: # ;
%<v%>%<pres%>%<p1%>%<pl%>:%>ן # ;
%<v%>%<pres%>%<p3%>%<pl%>:%>ן # ;

LEXICON V-HOBN-HO

%<v%>%<pres%>%<p2%>%<sg%>:%>סט # ;
%<v%>%<pres%>%<p3%>%<sg%>:%>ט # ;
%<v%>%<pres%>%<p2%>%<pl%>:%>ט # ;
%<v%>: V-COMMON-IMP-PL ;

LEXICON V-HOBN-HA

%<v%>%<pp%>%[%+pp%]:ט # ;

LEXICON VAux-COMMON-DEL

%<pres%>%<p2%>%<sg%>:%>סט # ;
%<pres%>%<p3%>%<sg%>:%>ט # ;
%<pres%>%<p2%>%<pl%>:%>ט # ;

LEXICON VAux-COMMON-NODEL

%<pres%>%<p1%>%<sg%>: # ;
%<pres%>%<p1%>%<pl%>:%>ן # ;
%<pres%>%<p3%>%<pl%>:%>ן # ;

LEXICON VAux-HOBN-HOB

%<vaux%>: V-COMMON-INF ;
%<vaux%>: VAux-COMMON-NODEL ;

LEXICON VAux-HOBN-HO

%<vaux%>: VAux-COMMON-DEL ;

LEXICON VAux-HOBN-HA

%<vaux%>%<pp%>%[%+pp%]:ט # ;

LEXICON VAux-VEL-VEL

%<vaux%>: VAux-COMMON-NODEL ;

LEXICON VAux-VEL-VE

%<vaux%>: VAux-COMMON-DEL ;

LEXICON VAux-REG-INFL

%<pres%>%<p1%>%<sg%>: # ;
%<pres%>%<p2%>%<sg%>:%>סט # ;
%<pres%>%<p3%>%<sg%>: # ;
%<pres%>%<p1%>%<pl%>:%>ן # ;
%<pres%>%<p2%>%<pl%>:%>ט # ;
%<pres%>%<p3%>%<pl%>:%>ן # ;

%<pp%>%[%+pp%]:ט # ;

LEXICON VAux-REG

%<vaux%>: V-COMMON-INF ;
%<vaux%>: VAux-REG-INFL ;

LEXICON CC

%<cnjcoo%>: # ;

LEXICON PREP-EN

PREP ;
%<pr%>%+דער%<det%>%<m%>%<acc%>:%>%{ע%}%{ן%} # ;
%<pr%>%+דער%<det%>%<m%>%<dat%>:%>%{ע%}%{ן%} # ;
%<pr%>%+דאָס%<det%>%<nt%>%<dat%>:%>%{ע%}%{ן%} # ;

LEXICON PREP

%<pr%>: # ;

LEXICON PRON-IND

%<prn%>%<ind%>: # ;

LEXICON PRON-P1-SG
איך%<prn%>%<pers%>%<p1%>%<sg%>%<nom%>:איך # ;
איך%<prn%>%<pers%>%<p1%>%<sg%>%<acc%>:מיך # ;
איך%<prn%>%<pers%>%<p1%>%<sg%>%<dat%>:מיר # ;

LEXICON PRON-P2-SG
דו%<prn%>%<pers%>%<p2%>%<sg%>%<nom%>:דו # ;
דו%<prn%>%<pers%>%<p2%>%<sg%>%<acc%>:דיך # ;
דו%<prn%>%<pers%>%<p2%>%<sg%>%<dat%>:דיר # ;

LEXICON PRON-P3-SG
ער%<prn%>%<pers%>%<p3%>%<sg%>%<m%>%<nom%>:ער # ;
ער%<prn%>%<pers%>%<p3%>%<sg%>%<m%>%<acc%>:אים # ;
ער%<prn%>%<pers%>%<p3%>%<sg%>%<m%>%<dat%>:אים # ;

זי%<prn%>%<pers%>%<p3%>%<sg%>%<f%>%<nom%>:זי # ;
זי%<prn%>%<pers%>%<p3%>%<sg%>%<f%>%<acc%>:זי # ;
זי%<prn%>%<pers%>%<p3%>%<sg%>%<f%>%<dat%>:איר # ;

עס%<prn%>%<pers%>%<p3%>%<sg%>%<nt%>%<nom%>:עס # ;
עס%<prn%>%<pers%>%<p3%>%<sg%>%<nt%>%<acc%>:עס # ;
עס%<prn%>%<pers%>%<p3%>%<sg%>%<nt%>%<dat%>:אים # ;

LEXICON PRON-P1-PL
מיר%<prn%>%<pers%>%<p1%>%<pl%>%<nom%>:מיר # ;
מיר%<prn%>%<pers%>%<p1%>%<pl%>%<acc%>:אונדז # ;
מיר%<prn%>%<pers%>%<p1%>%<pl%>%<dat%>:אונדז # ;

LEXICON PRON-P2-PL
איר%<prn%>%<pers%>%<p2%>%<pl%>%<nom%>:איר # ;
איר%<prn%>%<pers%>%<p2%>%<pl%>%<acc%>:אײַך # ;
איר%<prn%>%<pers%>%<p2%>%<pl%>%<dat%>:אײַך # ;

LEXICON PRON-P3-PL
זײ%<prn%>%<pers%>%<p3%>%<pl%>%<nom%>:זײ # ;
זײ%<prn%>%<pers%>%<p3%>%<pl%>%<acc%>:זײ # ;
זײ%<prn%>%<pers%>%<p3%>%<pl%>%<dat%>:זײ # ;

LEXICON ADJ-COMMON

%<pred%>: # ;

%<attr%>: ADJ-INFL-ENDINGS ;

LEXICON ADJ-INFL-ENDINGS

%<m%>%<sg%>%<nom%>:%>ער # ;
%<m%>%<sg%>%<acc%>:%>%{ע%}%{ן%} # ;
%<m%>%<sg%>%<dat%>:%>%{ע%}%{ן%} # ;
%<f%>%<sg%>%<nom%>:%>ע # ;
%<f%>%<sg%>%<acc%>:%>ע # ;
%<f%>%<sg%>%<dat%>:%>ער # ;
%<def%>%<nt%>%<sg%>%<nom%>:%>ע # ;
%<def%>%<nt%>%<sg%>%<acc%>:%>ע # ;
%<def%>%<nt%>%<sg%>%<dat%>:%>%{ע%}%{ן%} # ;
%<ind%>%<nt%>%<sg%>%<nom%>: # ;
%<ind%>%<nt%>%<sg%>%<acc%>: # ;
%<ind%>%<nt%>%<sg%>%<dat%>: # ;
! FIXME: figure out how to deal with "postpositive/nominalised" neuter forms,
!      e.g. at https://en.wiktionary.org/wiki/%D7%92%D7%A8%D7%95%D7%99%D7%A1
!      or https://en.wiktionary.org/wiki/%D7%A9%D7%98%D7%90%D6%B7%D7%A8%D7%A7
!      and how they're different from predicative forms
%<mfn%>%<pl%>%<nom%>:%>ע # ;
%<mfn%>%<pl%>%<acc%>:%>ע # ;
%<mfn%>%<pl%>%<dat%>:%>ע # ;

LEXICON ADJ-UML

%<adj%>%<comp%>:%>%{ь2%}ער ADJ-COMMON ;
%<adj%>%<sup%>:%>%{ь2%}%{ס%}ט ADJ-COMMON ;

%<adj%>: ADJ-COMMON ;

LEXICON ADJ

%<adj%>%<comp%>:%>ער ADJ-COMMON ;
%<adj%>%<sup%>:%>%{ס%}ט ADJ-COMMON ;

%<adj%>: ADJ-COMMON ;

LEXICON ADJ-BARE

%<adj%>: # ;

LEXICON ADJ-NO-COMP

%<adj%>: ADJ-COMMON ;

LEXICON ADJ-IRREG-COMP

%<adj%>%<comp%>:%>ער ADJ-COMMON ;
%<adj%>%<sup%>:%>%{ס%}ט ADJ-COMMON ;

LEXICON DET-PERS-AGREEMENT

%<attr%>%<m%>%<sg%>%<nom%>: # ;
%<attr%>%<m%>%<sg%>%<acc%>: # ;
%<attr%>%<m%>%<sg%>%<dat%>: # ;
%<attr%>%<f%>%<sg%>%<nom%>: # ;
%<attr%>%<f%>%<sg%>%<acc%>: # ;
%<attr%>%<f%>%<sg%>%<dat%>: # ;
%<attr%>%<nt%>%<sg%>%<nom%>: # ;
%<attr%>%<nt%>%<sg%>%<acc%>: # ;
%<attr%>%<nt%>%<sg%>%<dat%>: # ;
%<attr%>%<mfn%>%<pl%>%<nom%>:%>ע # ;
%<attr%>%<mfn%>%<pl%>%<acc%>:%>ע # ;
%<attr%>%<mfn%>%<pl%>%<dat%>:%>ע # ;

%<pred%>%<m%>%<sg%>%<nom%>:%>ער # ;
%<pred%>%<m%>%<sg%>%<acc%>:%>%{ע%}%{ן%} # ;
%<pred%>%<m%>%<sg%>%<dat%>:%>%{ע%}%{ן%} # ;
%<pred%>%<f%>%<sg%>%<nom%>:%>ע # ;
%<pred%>%<f%>%<sg%>%<acc%>:%>ע # ;
%<pred%>%<f%>%<sg%>%<dat%>:%>ער # ;
%<pred%>%<nt%>%<sg%>%<nom%>:%>ס # ;
%<pred%>%<nt%>%<sg%>%<acc%>:%>ס # ;
%<pred%>%<nt%>%<sg%>%<dat%>:%>%{ע%}%{ן%} # ;
%<pred%>%<mfn%>%<pl%>%<nom%>:%>ע # ;
%<pred%>%<mfn%>%<pl%>%<acc%>:%>ע # ;
%<pred%>%<mfn%>%<pl%>%<dat%>:%>ע # ;

LEXICON DET-PERS-P1SG

%<det%>%<pos%>: DET-PERS-AGREEMENT ;

LEXICON DET-PERS-P2SG

%<det%>%<pos%>: DET-PERS-AGREEMENT ;

LEXICON DET-PERS-P3SGMN

%<det%>%<pos%>: DET-PERS-AGREEMENT ;

LEXICON DET-PERS-P3SGF

%<det%>%<pos%>: DET-PERS-AGREEMENT ;

LEXICON DET-PERS-P1PL

%<det%>%<pos%>: DET-PERS-AGREEMENT ;

LEXICON DET-PERS-P2PL

%<det%>%<pos%>: DET-PERS-AGREEMENT ;

LEXICON DET-PERS-P3PL

%<det%>%<pos%>: DET-PERS-AGREEMENT ;

LEXICON DET-ITG

%<det%>%<itg%>: ADJ-INFL-ENDINGS ;

LEXICON DET-QNT

%<det%>%<qnt%>: # ;

LEXICON PRON-ITG

%<prn%>%<itg%>: # ;

LEXICON ADV-ITG

%<adv%>%<itg%>: # ;

LEXICON ADV

%<adv%>: # ;

LEXICON CNJSUB

%<cnjsub%>: # ;

LEXICON CNJADV

%<cnjadv%>: # ;

LEXICON PREADV

%<preadv%>: # ;

LEXICON NP-TOP

%<np%>%<top%>: # ;

LEXICON NP-ANT-M

%<np%>%<ant%>%<m%>: # ;

LEXICON NP-ANT-F

%<np%>%<ant%>%<f%>: # ;

LEXICON NUM

%<num%>: # ;

LEXICON NUM-ORD

%<num%>%<ord%>: # ;

LEXICON NUM-ORD-ADJ

%<num%>%<ord%>: # ;
%<num%>%<ord%>: ADJ-INFL-ENDINGS ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Conjunctions

אָבער:אָבער CC ; ! "but"
אָדער:אָדער CC ; ! "or"
און:און CC ; ! "and"
סײַ:סײַ CC ; ! "both/and"

אַז:אַז CNJSUB ; ! "that"
אַלס:אַלס CNJSUB ; ! "when"
אַפֿילו:אַפֿילו CNJSUB ; ! "even though; despite"
אױב:אױב CNJSUB ; ! "if"
װי:װי CNJSUB ; ! "how"
װען:װען CNJSUB ; ! "when"
װײַל:װײַל CNJSUB ; ! "because"


LEXICON Adverbs

װוּ:װאו ADV-ITG ; ! "where" ! Dir/LR
װוּ:װוּ ADV-ITG ; ! "where"
װוּהין:װאוהין ADV-ITG ; ! "whither"
װוּהין:װוּהין ADV-ITG ; ! "whither"
װי:װי ADV-ITG ; ! "how"
װען:װען ADV-ITG ; ! "when"

װי:װי PREADV ; ! "how"

אַזױ:אַזױ ADV ; ! "thus"
אַרױס:אַרױס ADV ; ! "out"
אַרײַן:אַרײַן ADV ; ! "in, inside"
אױך:אױך ADV ; ! "also"
אױס:אױס ADV ; ! "over (finished)"
דאָרט:דאָרט ADV ; ! "there"
הײַנט:הײַנט ADV ; ! "today"
מער:מער ADV ; ! "more"
נאָך:נאָך ADV ; ! "still"
נאָך:נאָך PREADV ; ! "still"  ! FIXME: CHECK
נאָר:נאָר PREADV ; ! "only"
נישט:ניט ADV ; ! "not" ! Dir/LR
נישט:נישט ADV ; ! "not"
פֿאַראַן:פֿאַראַן ADV ; ! "there"
צוזאַמען:צוזאַמען ADV ; ! "together"
צוריק:צוריק ADV ; ! "back"
שױן:שױן ADV ; ! "already, yet"

LEXICON Prepositions

אַלס:אַלס PREP ; ! "as"
אַרום:אַרום PREP ; ! "around"
אָן:אָן PREP ; ! "without"
אום:אום PREP ; ! "around; over"
אונטער:אונטער PREP ; ! "under"
איבער:איבער PREP ; ! "over, above; about"
אין:אין PREP-EN ; ! "in"
אױף:אױף PREP-EN ; ! "on, by"
ביז:ביז PREP ; ! "up to, until"
בײַ:בײַ PREP-EN ; ! "у"
דורך:דורך PREP ; ! "through"
זינט:זינט PREP ; ! "since"
זינט:זײַט PREP ; ! "since"
זינט:צײַט PREP ; ! "since"
כּדי:כּדי PREP ; ! "in order (to)"
לעבן:לעבן PREP ; ! "next to"
לעבן:נעבן PREP ; ! "next to"
לױט:לױט PREP ; ! "according to"
מיט:מיט PREP-EN ; ! "with"
נאָך:נאָך PREP-EN ; ! "after"
פֿאַר:פֿאַר PREP-EN ; ! "for, per"
פֿון:פֿון PREP-EN ; ! "of, from"
צו:צו PREP-EN ; ! "to"
צוליב:צוליב PREP ; ! "because of"
צװישן:צװישן PREP ; ! "between"
קעגן:קעגן PREP ; ! "against"
קײן:קײן PREP ; ! "to (+place)"
װי:װי PREP ; ! "like"
װעגן:װעגן PREP ; ! "about"


LEXICON DetRoot

דער:ד DET-DEF-M ; ! "the"
די:ד DET-DEF-F ; ! "the"
דאָס:ד DET-DEF-N ; ! "the"
די:ד DET-DEF-PL ; ! "the"
אַ:אַ DET-INDEF ; ! "a"
אַ:אַן DET-INDEF ; ! "an" ! Dir/LR
קײן:קײן DET-INDEF ; ! "a/an, no, any"

מײַן:מײַן DET-PERS-P1SG ;
דײַן:דײַן DET-PERS-P2SG ;
זײַן:זײַן DET-PERS-P3SGMN ;
איר:איר DET-PERS-P3SGF ;
אונדזער:אונדזער DET-PERS-P1PL ;
אײַער:אײַער DET-PERS-P2PL ;
זײער:זײער DET-PERS-P3PL ;

װעלך:װעלך DET-ITG ; ! "which"
אַלע:אַלע DET-QNT ; ! "all"
אַ% סך:אַ% סך DET-QNT ; ! "a lot"
אַ% סך:אַסאַך DET-QNT ; ! "a lot"
עטלעכע:עטלעכע DET-QNT ; ! "some (a few)"


LEXICON Pronouns

PRON-P1-SG ;
PRON-P2-SG ;
PRON-P3-SG ;
PRON-P1-PL ;
PRON-P2-PL ;
PRON-P3-PL ;
מען%<prn%>%<pers%>%<impers%>%<nom%>:מען # ;
מען%<prn%>%<pers%>%<impers%>%<nom%>:מע # ;  ! Dir/LR
װאָס%<prn%>%<itg%>%<nom%>:װאָס # ;
װאָס%<prn%>%<itg%>%<acc%>:װאָס # ;
װאָס%<prn%>%<itg%>%<dat%>:װאָס # ;
זיך%<prn%>%<ref%>:זיך # ;

װעלך:װעלך PRON-ITG ; ! "which one (?)"  FIXME: CHECK

גאָרנישט:גאָרנישט PRON-IND ;  ! "nothing"
גאָרנישט:גאָרניט PRON-IND ;   ! Dir/LR
אַלץ:אַלץ PRON-IND ;          ! "everything"
עפּעס:עפּעס PRON-IND ;        ! "something"


LEXICON NounRoot

אַפּריל:אַפּריל N-M-N ; ! "April"
אַרבעט:אַרבעט N-F-S ; ! "work"
אָקטאָבער:אָקטאָבער N-M-S ; ! "October"
אױגוסט:אױגוסט N-M-N ; ! "August" CHECK: PL in N?
אײַזנבאַן:אײַזנבאַן N-F-N ; ! "railroad"
בּאַן:בּאַן N-F-N ; ! "train, railroad"
בּאַרג:בּאַרג N-M-UML ; ! "mountain"
בּױם:בּ%{ױ%}ם N-M-UML-ER ; ! "tree" plural with ײ
באַפֿעלקערונג:באַפֿעלקערונג N-F-N ; ! "population"
בוך:בוך N-N-UML-ER ; ! "book"
געלט:געלט N-N-UML-ER ; ! "money"  NOTE: no umlaut [possible]
געשיכטע:געשיכטע N-F-S ; ! "history"
דעצעמבער:דעצעמבער N-M-S ; ! "December"
דעצעמבער:דעצעמבער N-M-S ; ! "December"
דרום:דרום N-M-NOPL ; ! "south"
הימל:הימל N-M-N ; ! "heaven"
היסטאָריע:היסטאָריע N-F-S ; ! "history"
הױז:הױז N-N-UML-ER ; ! "house" plural with ײַ
זון:זון N-F-N ; ! "sun"
זון:זון N-M-UML ; ! "son"
חסיד:חסיד N-M-HEB ; ! "khosid (Hassid)"
חתונה:חתונ N-F-HEB ; ! "wedding"
טאָג:טאָג N-M-UML ; ! "day"
טאָכטער:טאָכטער N-F-UML ; ! "daughter"
טײל:טײל N-F-N ; ! "part"
יאַנואַר:יאַנואַר N-M-N ; ! "January"
יאָר:יאָר N-M-N ; ! "year"
יולי:יולי N-M-S ; ! "July"
יוני:יוני N-M-S ; ! "June"
ייִד:איד N-M-N ; ! "Jew" ! Dir/LR
ייִד:ייִד N-M-N ; ! "Jew"
ישיבה:ישיב N-F-HEB ; ! "yeshiva"
לאַן:לאַן N-M-N ; ! "meadow"
לאַנד:לאַנד N-N-UML-ER ; ! "country, land"
לעבן:לעבן N-N-S ; ! "life"
מאַרץ:מאַרץ N-M-N ; ! "March"
מזרח:מזרח N-M-NOPL ; ! "east"
מיטן:מיטן N-M-S ; ! "middle"
מלחמה:מלחמ N-F-HEB ; ! "war"
מענטש:מענטש N-M-N ; ! "human; mentsh, gentleman"
מערבֿ:מערבֿ N-M-NOPL ; ! "west"
מײַ:מײַ N-N-N ; ! "May" CHECK: PL in N?
נאָמען:נאָמען N-M-UML ; ! "name"  ! FIXME: irreg alternative plural נעמענער
נאָװעמבער:נאָװעמבער N-M-S ; ! "November"
סטאַנציע:סטאַנציע N-F-S ; ! "station"
סעפּטעמבער:סעפּטעמבער N-M-S ; ! "September" CHECK: PL in S?
ספֿר:ספֿר N-M-HEB ; ! "seyfer (religious book)"
פּאָיעזד:פּאָיעזד N-M-N ; ! "train"
פֿאָטער:פֿאָטער N-M-S ; ! "father"
פֿעברואַר:פֿעברואַר N-M-N ; ! "February" CHECK: PL in N?
פֿרײַנד:פֿרײַנד N-M-NULL ; ! "friend"
צוג:צוג N-M-N ; ! "train"
צפֿון:צפֿון N-M-NOPL ; ! "north"
צײַט:צײַט N-F-N ; ! "time"
קאַלענדאָר:קאַלענדאַר N-M-N ; ! "calendar"
קינד:קינד N-N-UML-ER ; ! "child" (technically no umlaut)
קריג:קריג N-F-N ; ! "war"
רבֿ:רבֿ N-M-HEB-N ; ! "rov (rabbi)" (pl: rabonem)
רבי:רבי N-M-REBE ; ! "rebe (rabbi)"
רעגירונג:רעגירונג N-F-N ; ! "government"
שטאַט:שטאַט N-M-N ; ! "state"
שטאָט:שטאָט N-F-UML ; ! "city"
שװעסטער:שװעסטער N-F-NULL ; ! "sister"
תּורה:תּורה N-F-NOPL ; ! "Torah (Hebrew bible)"
װאַסער:װאַסער N-N-N ; ! "water"
װעג:װעג N-M-N ; ! "way"
װעלט:װעלט N-F-N ; ! "world"

!FIXME: how to deal with words like the following (compounds, plural on first word)
!בית החיים   ! "cemetery"
!בית הקברות  ! "cemetery"
!בית עולם    ! "cemetery"
!בית עלמין   ! "cemetery"


LEXICON VerbRoot

%[%-pp%]:גע VerbStems ; ! past participle
VerbStems ; ! other forms

%[%-←on%]%[%-pp%]:אָנגע VerbStems ; ! past participle
%[%-→on%]: VerbStems ; ! other forms
%[%-←on%]:אָנ VerbStems ; ! other forms

LEXICON IrregularVerbs

אָנטאָן%[√on%]:טאָ V-TON-TO ; ! "put on clothes"
אָנטאָן%[√on%]:טו V-TON-TU ; ! "put on clothes"
טאָן:טאָ V-TON-TO ; ! "do"
טאָן:טו V-TON-TU ; ! "do"

זײַן:זײַ V-ZAYN-ZAY ;
זײַן:בי V-ZAYN-BI ;
זײַן:איז V-ZAYN-IZ ;
זײַן:װע V-ZAYN-VE ;
זײַן:זע V-ZAYN-ZE ; ! Dir/LR

האָבן:האָב V-HOBN-HOB ;
האָבן:האָ V-HOBN-HO ;
האָבן:האַ V-HOBN-HA ;

גײן:גאַנג V-STRONG-PP-ONLY ; ! "go"
גײן:גײ V-GEYN-GEY ;          ! "go"
גײן:גײע V-GEYN-GEYE ;        ! "go"


LEXICON Auxiliaries

האָבן:האָב VAux-HOBN-HOB ; ! "have"
האָבן:האָ VAux-HOBN-HO ;
האָבן:האַ VAux-HOBN-HA ;

װעל:װעל VAux-VEL-VEL ; ! "will"
װעל:װע VAux-VEL-VE ;

דאַרפֿן:דאַרף VAux-REG ; ! "have to, ought to, must"
זאָלן:זאָל VAux-REG ; ! "should"
קענען:קענ VAux-REG ; ! "can"

LEXICON VerbStems

IrregularVerbs ; 
Auxiliaries ; 

אַרבעטן:אַרבעט V-REG ; ! "work"
אָנהײבן%[√on%]:הױב V-STRONG-PP-ONLY ; ! "begin"
אָנהײבן%[√on%]:הײב V-STRONG-NOPP ; ! "begin"
בּעטן:בּעט V-STRONG-7 ; ! "ask"
באַקומען:באַקום V-REG-NOPREF ; ! "receive, acquire"
בלײַבן:בליב V-STRONG-PP-ONLY ; ! "stay"
בלײַבן:בלײַב V-STRONG-NOPP ; ! "stay"
געפֿינען:געפֿין V-STRONG-NOPP ; ! "find"
געפֿינען:פֿון V-STRONG-PP-ONLY ; ! "find"
דאַרפֿן:דאַרף V-REG ; ! "need, require"
הײסן:הײס V-STRONG-7 ; ! "be called"
זאָגן:זאָג V-REG ; ! "say"
לעבן:לעב V-REG ; ! "live"
מאַכן:מאַך V-REG ; ! "make"
נעמען:נום V-STRONG-PP-ONLY ; ! "took"
נעמען:נעם V-STRONG-NOPP ; ! "take"
עסן:געס V-STRONG-PP-ONLY ; ! "eat"
עסן:עס V-STRONG-NOPP ; ! "eat"
פֿאַרלירן:פֿאַרליר V-REG-NOPREF ; ! "lose"
פֿאָרן:פֿאָר V-STRONG-7 ; ! "ride, drive, travel by animal/vehicle"
קומען:קום V-STRONG-7 ; ! "come"
קענען:קענ V-REG ; ! "know"
קױפֿן:קױפֿ V-REG ; ! "buy"
רעדן:רעד V-REG ; ! "speak"
שרײַבן:שריב V-STRONG-PP-ONLY ; ! "write"
שרײַבן:שרײַב V-STRONG-NOPP ; ! "write"
װערן:װאָר V-STRONG-PP-ONLY ; ! "become"
װערן:װער V-STRONG-NOPP ; ! "become"

LEXICON Adjectives

אַלט:אַלט ADJ-UML ; ! "old"
אַנדער:אַנדער ADJ-NO-COMP ; ! "different"
גוט:בעס ADJ-IRREG-COMP ; ! "better/best"
גוט:גוט ADJ-NO-COMP ; ! "good"
געבױרן:געבױרן ADJ-BARE ; ! "born"
גרױס:גרױס ADJ-UML ; ! "big"
היסטאָריש:היסטאָריש ADJ-NO-COMP ; ! "historical" FIXME: CHECK NO-COMP?
הױפּט:הױפּט ADJ-NO-COMP ; ! "head, main"
יונג:יונג ADJ-UML ; ! "young"
פּאָפּולער:פּאָפּולער ADJ ; ! "popular"
שטאַרק:שטאַרק ADJ ; ! "strong"
שלעכט:ערג ADJ-IRREG-COMP ; ! "worse/worst"
שלעכט:שלעכט ADJ-NO-COMP ; ! "bad"
שײן:שײן ADJ-UML ; ! "beautiful"
בּאַקאַנט:בּאַקאַנט ADJ-NO-COMP ; ! "well known" FIXME: CHECK NO-COMP?

LEXICON ProperNouns

אַמעריקע:אַמעריקע NP-TOP ; ! "America"
אײראָפּע:אײראָפּע NP-TOP ; ! "Europe"
דײַטשלאַנד:דײַטשלאַנד NP-TOP ; ! "Germany"
ירושלים:ירושלים NP-TOP ; ! "Jerusalem"
ישראל:ישראל NP-TOP ; ! "Israel"
ארץ% ישראל:ארץ% ישראל NP-TOP ; ! "Land of Israel"  ! FIXME: Maybe just N?
לאָס% אַנדזשעלעס:לאָס% אַנדזשעלעס NP-TOP ; ! "Los Angeles"
ניו% יאָרק:ניו% יאָרק NP-TOP ; ! "New York"
ניו% יאָרק:ניו%-יאָרק NP-TOP ; ! "New York" ! Dir/LR
ניו% יאָרק:ניו%־יאָרק NP-TOP ; ! "New York" ! Dir/LR
ענגלאַנד:ענגלאַנד NP-TOP ; ! "England"
קאַנאַדע:קאַנאַדע NP-TOP ; ! "Canada"
רוסלאַנד:רוסלאַנד NP-TOP ; ! "Russia"

אַבֿרהם:אַבֿרהם NP-ANT-M ; ! "Avrohem"
אהרן:אהרן NP-ANT-M ; ! "Arn"
דוד:דוד NP-ANT-M ; ! "Dovid"
יוסף:יוסף NP-ANT-M ; ! "Yo(y)sef"
יעקבֿ:יעקבֿ NP-ANT-M ; ! "Yankev"
יצחק:יצחק NP-ANT-M ; ! "Yitskhok"
ישראל:ישראל NP-ANT-M ; ! "Yisroel"
מאיר:מאיר NP-ANT-M ; ! "Meyer"
מנחם:מנחם NP-ANT-M ; ! "Menakhem"
מענדל:מענדל NP-ANT-M ; ! "Mendl"
משה:משה NP-ANT-M ; ! "Moyshe"
נחמן:נחמן NP-ANT-M ; ! "Nakhman"

LEXICON Numerals

אײנס:אײנס NUM ;  ! FIXME: counting 1
אײן:אײן NUM ;    ! FIXME: number one; also TODO: inflected forms of אײן
צװײ:צװײ NUM ;         ! "two"
דרײַ:דרײַ NUM ;         ! "three"
פֿיר:פֿיר NUM ;         ! "four"
פֿינף:פֿינף NUM ;       ! "five"
זעקס:זעקס NUM ;       ! "six"
זיבן:זיבן NUM ;       ! "seven"
אַכט:אַכט NUM ;         ! "eight"
נײַן:נײַן NUM ;         ! "nine"
צען:צען NUM ;         ! "ten"
עלף:עלף NUM ;         ! "eleven"
צװעלף:צװעלף NUM ;     ! "twelve"
דרײַצן:דרײַצן NUM ;     ! "thirteen"
פֿערצן:פֿערצן NUM ;     ! "fourteen"
פֿופֿצן:פֿופֿצן NUM ;     ! "fifteen"
זעכצן:זעכצן NUM ;     ! "sixteen"
זיבעצן:זיבעצן NUM ;   ! "seventeen"
אַכצן:אַכצן NUM ;       ! "eighteen"
נײַנצן:נײַנצן NUM ;     ! "nineteen"
צװאַנציק:צװאַנציק NUM ; ! "twenty"
דרײַסיק:דרײַסיק NUM ;   ! "thirty"
פֿערציק:פֿערציק NUM ;   ! "forty"
פֿופֿציק:פֿופֿציק NUM ;   ! "fifty"
זעכציק:זעכציק NUM ;   ! "sixty"
זיבעציק:זיבעציק NUM ; ! "seventy"
אַכציק:אַכציק NUM ;     ! "eighty"
נײַנציק:נײַנציק NUM ;   ! "ninety"
הונדערט:הונדערט NUM ; ! "hundred"
טויזנט:טויזנט NUM ;   ! "thousand"    ! FIXME: plural טױזנטער
מיליאָן:מיליאָן NUM ;   ! "million"     ! FIXME: plural מיליאָנען

אײנס:ערשט NUM-ORD-ADJ ;
צװײ:צװײט NUM-ORD-ADJ ;
דרײַ:דריט NUM-ORD-ADJ ;
פֿיר:פֿערט NUM-ORD-ADJ ;
פֿינף:פֿינפֿט NUM-ORD-ADJ ;
זעקס:זעקסט NUM-ORD-ADJ ;
זיבן:זיבעט NUM-ORD-ADJ ;
אַכט:אַכט NUM-ORD-ADJ ;
נײַן:נײַנט NUM-ORD-ADJ ;
צען:צענט NUM-ORD-ADJ ;             ! "tenth"
עלף:עלפֿט NUM-ORD-ADJ ;             ! "eleventh"
צװעלף:צוועלפֿט NUM-ORD-ADJ ;        ! "twelfth"
דרײַצן:דרײַצנט NUM-ORD-ADJ ;         ! "thirteenth"
פֿערצן:פֿערצנט NUM-ORD-ADJ ;         ! "fourteenth"
פֿופֿצן:פֿופֿצנט NUM-ORD-ADJ ;         ! "fifteenth"
זעכצן:זעכצנט NUM-ORD-ADJ ;         ! "sixteenth"
זיבעצן:זיבעצנט NUM-ORD-ADJ ;       ! "seventeenth"
אַכצן:אַכצנט NUM-ORD-ADJ ;           ! "eighteenth"
נײַנצן:נײַנצנט NUM-ORD-ADJ ;         ! "nineteenth"
צװאַנציק:צװאַנציקסט NUM-ORD-ADJ ;    ! "twentieth"
דרײַסיק:דרײַסיקסט NUM-ORD-ADJ ;      ! "thirtieth"
פֿערציק:פֿערציקסט NUM-ORD-ADJ ;      ! "fortieth"
פֿופֿציק:פֿופֿציקסט NUM-ORD-ADJ ;      ! "fiftieth"
זעכציק:זעכציקסט NUM-ORD-ADJ ;      ! "sixtieth"
זיבעציק:זיבעציקסט NUM-ORD-ADJ ;    ! "seventieth"
אַכציק:אַכציקסט NUM-ORD-ADJ ;        ! "eightieth"
נײַנציק:נײַנציקסט NUM-ORD-ADJ ;      ! "ninetieth"
הונדערט:הונדערטסט NUM-ORD-ADJ ;    ! "hundredth"
טויזנט:טויזנטסט NUM-ORD-ADJ ;      ! "thousandth"

LEXICON Titles

הרבֿ%<title%>:הרבֿ # ; ! "rabbi"
רב%<title%>:רב # ; ! "reb (mister)"
מרת%<title%>:מרת # ; ! "Mrs."

LEXICON Punctuation

%.%<sent%>:%. # ;
%.%.%<sent%>:%.%. # ;
%.%.%.%<sent%>:%.%.%. # ;
…%<sent%>:… # ; 
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%-%-%<guio%>:%-%- # ;
%-%<guio%>:%- # ;
%−%<guio%>:%− # ; 
%—%<guio%>:%— # ;
%–%<guio%>:%– # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ;
%'%<apos%>:%' # ;
%"%<quot%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
%\%<sent%>:%\ # ;
%/%<sent%>:%/ # ;


LEXICON Digits ! Use/Circ

%0:%0 NUM ;  ! Use/Circ
№:№ DIGITLEX ;  ! Use/Circ
№% :№%  DIGITLEX ;  ! Use/Circ
DIGITLEX ; ! Use/Circ
<[%- | %− | %+ ]> DIGITLEX ; ! Use/Circ
LASTDIGIT ; ! Use/Circ
LASTDIGIT-REST ; ! Use/Circ



! Resources:
! https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! https://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! https://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! http://hfst.sourceforge.net/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! https://wiki.apertium.org/wiki/Hfst

